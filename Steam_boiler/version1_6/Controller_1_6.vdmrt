class Controller_1_6

instance variables
    public ctl_qe : real;  -- water in container
    public ctl_pump_state :  SteamBoiler_1_6`PumpState;
    public ctl_valve_state : SteamBoiler_1_6`ValveState; 

operations

    public Controller_1_6: () ==> Controller_1_6
    Controller_1_6() == (
        ctl_qe := 0;
        ctl_pump_state := <On>;
        ctl_valve_state := <Open>; 
    );

    public MonitorWaterQuantity:() ==> ()
    MonitorWaterQuantity() == duration(0)(
        ctl_qe := mySys`boiler.WaterQuantity();
    );

    public EnsureSafeWaterQuantity: () ==> ()
    EnsureSafeWaterQuantity() == duration(0)(
        MonitorWaterQuantity();
        if ctl_qe < mySys`boiler.N1 then(
            SetValveState(<Open>);
            SetPumpState(<On>);
        ) else if ctl_qe > mySys`boiler.N2 then(
            SetValveState(<Open>);
            SetPumpState(<Off>);
        ) else (
            SetValveState(<Closed>);
            SetPumpState(<Off>); 
        );         
    );


    public SetValveState: SteamBoiler_1_6`ValveState ==> ()
    SetValveState(vs) == duration(0)(
        mySys`boiler.SetValveState(vs);
        ctl_valve_state := mySys`boiler.GetValveState(); 
    ); 

    public SetPumpState: SteamBoiler_1_6`PumpState==>()
    SetPumpState(ps) == duration(0)(
        mySys`boiler.SetPumpState(ps);
        ctl_pump_state := mySys`boiler.GetPumpState(); 
    ); 

thread
    periodic(2E9,0,0,0)(EnsureSafeWaterQuantity);

end Controller_1_6

