class RobotArm_2   

instance variables

    public env_angle : real;       -- angle of arm in radians
    public env_angle_vel : real;   -- velocity of arm in rad/Sec
    public env_angle_acc : real;   -- acceleration of arm angle in rad/sec^2 

    public last_time : real;  -- A record of the last time the operation (in this case step) was called

    public final_sim_time : real;

    public state_history : seq of (real * real * real); -- time * env_angle * env_angle_vel

values
    M1 : real = -0.2;   -- Minimum limit for arm
    M2 : real = 1.77;   -- Maximum limit for arm
    I : real = 1;     -- Inertia of robot arm should be calculated: I = m*r^2
    m : real = 0.1;     -- Mass 
    g : real = 9.82;    -- Gravitational force
    r : real = 1;       -- Length


operations
    public RobotArm_2: () ==> RobotArm_2
    RobotArm_2() == (
        env_angle := 0;
        env_angle_vel := 0;
        env_angle_acc := 0;

        last_time := -1E4;

        final_sim_time := 0;

        state_history := [];
    );

    public adjustArmAngle: (real) ==> ()
    adjustArmAngle(dqe) == duration(0)(
        dcl t: real := time/1E9;
        dcl dt: real := t-last_time;
        if dt <> 0 then (
            env_angle_vel := dqe / dt; 
        );
        
        env_angle := env_angle + dqe;
        state_history := state_history ^ [mk_(t, env_angle, env_angle_vel)];
        last_time := t;
    );

    public getArmAngle : () ==> (real)
    getArmAngle() == duration(0)(
        return env_angle;
    );

    public getArmVel : () ==> (real)
    getArmVel() == duration(0)(
        return env_angle_vel;
    );

    public setFinalSimTime : (real) ==> ()
    setFinalSimTime(given_time) == duration(0)(
        final_sim_time := given_time;
    );

    public isFinished : () ==> ()
     isFinished() == skip

sync
    per isFinished => (time > final_sim_time);

end RobotArm_2